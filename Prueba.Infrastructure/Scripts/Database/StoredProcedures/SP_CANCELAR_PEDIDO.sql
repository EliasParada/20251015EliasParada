USE DB_PRUEBA_TECNICA;
GO

IF OBJECT_ID('SP_CANCELAR_PEDIDO', 'P') IS NOT NULL
    DROP PROCEDURE SP_CANCELAR_PEDIDO;
GO

CREATE PROCEDURE SP_CANCELAR_PEDIDO
    @ID_PEDIDO INT
AS
BEGIN
    SET NOCOUNT ON;

    DECLARE @ESTADO VARCHAR(20);
    SELECT @ESTADO = ESTADO FROM PEDIDO WHERE ID_PEDIDO = @ID_PEDIDO;

    IF @ESTADO NOT IN ('PENDIENTE', 'CONFIRMADO')
    BEGIN
        RAISERROR('No se puede cancelar el pedido en este estado.', 16, 1);
        RETURN;
    END

    BEGIN TRY
        BEGIN TRAN;

        UPDATE P
           SET P.STOCK = P.STOCK + D.CANTIDAD
          FROM PRODUCTO P
    INNER JOIN DETALLE_PEDIDO D ON D.ID_PRODUCTO = P.ID_PRODUCTO
         WHERE D.ID_PEDIDO = @ID_PEDIDO;

        INSERT INTO BITACORA_INVENTARIO (ID_PRODUCTO, ACCION, CANTIDAD)
        SELECT ID_PRODUCTO, 'ENTRADA', CANTIDAD
          FROM DETALLE_PEDIDO
         WHERE ID_PEDIDO = @ID_PEDIDO;

        UPDATE PEDIDO
           SET ESTADO = 'CANCELADO',
               FECHA_ACTUALIZACION = GETDATE()
         WHERE ID_PEDIDO = @ID_PEDIDO;

        COMMIT TRAN;
    END TRY
    BEGIN CATCH
        ROLLBACK TRAN;
        THROW;
    END CATCH;
END
GO
